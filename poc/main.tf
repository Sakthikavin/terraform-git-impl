terraform {
  required_providers {
    github = {
      source  = "integrations/github"
      version = "~> 6.0"
    }
  }
}

############################
# GitHub Provider (OAuth Token)
############################
provider "github" {
  owner = var.github_org
  token = var.github_token
}

############################
# Get main branch SHA
############################
data "github_branch" "main" {
  repository = var.repository
  branch     = "main"
}

############################
# Create feature branch
############################
resource "github_branch" "ads_branch" {
  repository = var.repository
  branch     = "feature/add-ads-config-13"
  source_sha = data.github_branch.main.sha
}

############################
# Create ADS JSON file
############################
resource "github_repository_file" "ads_definition" {
  repository = var.repository
  branch     = github_branch.ads_branch.branch
  file       = "ads/attach-data-source.json"

  content = jsonencode({
    ruleType = "AttachDataSource"
    state = {
      dev = "active"
    }
    team          = "StrawHats"
    operatingTeam = "StrawHats"
    metadata      = {}
    dataAccess = {
      dev = {
        "sakthikavin.ss@thoughtworks.com" = "user"
      }
    }
    localTokenizationPolicy = {}
    sourceAdapter = {
      adapterType = "Kafka"
      parameters  = {}
    }
  })

  commit_message      = "Add AttachDataSource ADS configuration"
  commit_author       = var.commit_author
  commit_email        = var.commit_email
  overwrite_on_create = true
}

###################################
# Create dummy GitHub Actions flow
###################################
resource "github_repository_file" "dummy_workflow" {
  repository = var.repository
  branch     = github_branch.ads_branch.branch
  file       = ".github/workflows/dummy.yml"

  content = <<-EOT
    name: Dummy Workflow

    on:
      pull_request:
        branches: [ main ]
      push:
        branches: [ main ]

    jobs:
      dummy-job:
        runs-on: ubuntu-latest
        steps:
          - name: Checkout code
            uses: actions/checkout@v3

          - name: Print message
            run: echo "This is a dummy GitHub Action created by Terraform"
  EOT

  commit_message      = "Add dummy GitHub Actions workflow"
  commit_author       = var.commit_author
  commit_email        = var.commit_email
  overwrite_on_create = true
}

############################
# Create Pull Request
############################
resource "github_repository_pull_request" "ads_pr" {
  base_repository = var.repository
  title           = "Add AttachDataSource ADS configuration"

  body = <<-EOT
    ## New ADS Configuration

    **Rule Type:** AttachDataSource  
    **Team:** StrawHats  
    **Source Adapter:** Kafka  
    
    ---
    **Requested by:** ${var.commit_author} <${var.commit_email}>  
    Auto generated by Terraform
  EOT

  base_ref = "main"
  head_ref = github_branch.ads_branch.branch

  depends_on = [
    github_repository_file.ads_definition,
    github_repository_file.dummy_workflow
  ]
}

############################
# Comment on Pull Request
############################
# Note: github_issue_comment is not supported in the GitHub provider
# Comments on PRs need to be added via API or manually after creation or through github actions.

############################
# Outputs
############################
output "pull_request_url" {
  value = "https://github.com/${var.github_org}/${var.repository}/pull/${github_repository_pull_request.ads_pr.number}"
}
