terraform {
  required_providers {
    github = {
      source  = "integrations/github"
      version = "~> 6.0"
    }
  }
}

#  Use GitHub Provider with App Authentication
# Added the private key file in github secrets which is consumed by terraform when triggered using github actions
# For local testing, save the private key as 'private-key.pem' in the same directory as this terraform file
provider "github" {
  owner = var.github_org

  app_auth {
    id              = var.app_id
    installation_id = var.app_installation_id
    pem_file        = file("private-key.pem")
  }
}

# Get main branch SHA
data "github_branch" "main" {
  repository = var.repository
  branch     = "main"
}

# Create feature branch
resource "github_branch" "dataproduct_branch" {
  repository = var.repository
  branch     = "feature/add-${var.data_product.id}"
  source_sha = data.github_branch.main.sha
}

# Create YAML file on the new branch
resource "github_repository_file" "dataproduct_definition" {
  repository  = var.repository
  branch      = github_branch.dataproduct_branch.branch
  file        = "data-products/${var.data_product.id}.yaml"

  content = yamlencode({
    id           = var.data_product.id
    domain       = var.data_product.domain
    display_name = var.data_product.display_name
    description  = var.data_product.description
    assets = [
      for asset in var.data_product.assets : { urn = asset }
    ]
  })

  commit_message      = "Add ${var.data_product.display_name} data product definition"
  commit_author       = "Data Product Automation"
  commit_email        = "automation@company.com"
  overwrite_on_create = true
}

# Create PR
resource "github_repository_pull_request" "dataproduct_pr" {
  base_repository  = var.repository
  title       = "Add ${var.data_product.display_name} Data Product"

  body = <<-EOT
    ## New Data Product

    ID: `${var.data_product.id}`
    Domain: ${var.data_product.domain}
    Description: ${var.data_product.description}

    ### Assets Included
    ${join("\n", [for asset in var.data_product.assets : "- ${asset}"])}

    ---
    Auto generated by Terraform
  EOT

  base_ref = "main"
  head_ref = github_branch.dataproduct_branch.branch

  depends_on = [github_repository_file.dataproduct_definition, github_repository_file.dummy_workflow]
}

resource "github_repository_file" "dummy_workflow" {
  repository  = var.repository
  branch      = github_branch.dataproduct_branch.branch
  file        = ".github/workflows/dummy.yml"

  content = <<-EOT
    name: Dummy Workflow

    on:
      pull_request:
        branches: [ main ]
      push:
        branches: [ main ]

    jobs:
      dummy-job:
        runs-on: ubuntu-latest
        steps:
          - name: Checkout code
            uses: actions/checkout@v3

          - name: Print message
            run: echo "This is a dummy GitHub Action created by Terraform"
  EOT

  commit_message = "Add dummy GitHub Actions workflow"
  commit_author  = "Terraform Automation"
  commit_email   = "automation@example.com"

    overwrite_on_create = true
}

output "pull_request_url" {
  value = "https://github.com/${var.github_org}/${var.repository}/pull/${github_repository_pull_request.dataproduct_pr.number}"
}
